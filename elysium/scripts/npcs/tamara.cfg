speech_tamara_sorry = (registersound "elysium/speech/Tamara/Sorry_but_no.ogg")
speech_tamara_unfortunetly = (registersound "elysium/speech/Tamara/Unfortunetly_no.ogg")
speech_tamara_resently = (registersound "elysium/speech/Tamara/Resently.ogg")
speech_tamara_Only_to_TC37 = (registersound "elysium/speech/Tamara/Only_to_TC37.ogg")
speech_tamara_Double_Ice_Goog = (registersound "elysium/speech/Tamara/Double_Ice_Goog.ogg")
speech_tamara_Blue_Hoverbike = (registersound "elysium/speech/Tamara/Blue_Hoverbike.ogg")
speech_tamara_Wierdo_descr = (registersound "elysium/speech/Tamara/Wierdo_descr.ogg")

newgui Tamara [
    guititle "Tamara"
    if (= $tamara_asked 0)[
        guibutton "Do you know something about Nana?" "btActionTamara_1"
    ][
        guibutton "Do you know something new about the wierdos?" "btActionTamara_2"
    ]
    guibutton "Fine ... I think" "btActionTamara_0"
]

newgui Tamara_2 [
    guitext "Unforunetly no."
    guitext "But I saw her yesterday talking to some wierdos."
    guitext "I went to her, because she looked a little bit scared."
    guitext "I ask her about the guys, but she didn't told me anything"
    guitext "about them."
    guitext "Can I do something else for you?"
    guibutton "No, but thank you Tamara." "btActionTamara_0"
]

newgui Tamara_3 [
    guitext "I'm sorry hun, but no."
    guibutton "Okay, bye then ..." "btActionTamara_0"
]

newgui Tamara_4 [
    guitext "Hey, I resently saw one of the wierdos in here."
    guitext "I talked to him and tried to find something out about him."
    guitext "But he was realy strange and silent."
    guitext "All I was able to find out was his name, Gerald."
    if (= $tamara_asked_1 0)[
        guibutton "Did he talked to anyone else?" "btActionTamara_3"
    ]
    if (= $tamara_asked_2 0)[
        guibutton "Did he ordred something special?" "btActionTamara_5"
    ]
    if (= $tamara_asked_3 0)[
        guibutton "What did he looked like?" "btActionTamara_6"
    ]
    if (= $tamara_asked_4 0) [
        guibutton "Where did he went?" "btActionTamara_7"
    ]    
    guibutton "Okay, thank you very much Tamara." "btActionTamara_0"
]

newgui Tamara_5 [ //Did he talked to anyone else?
    guitext "No, only to TC37 to order some drink."
    guibutton "Ask more .." "btActionTamara_4"
]

newgui Tamara_6 [ //Did he ordred something special?
    guitext "Uhm ..., as far as I remember her wanted to have a Double Iced Goog."
    guibutton "Ask more .." "btActionTamara_4"
]

newgui Tamara_7 [
    guitext "I didn't saw much of him, because he was hooded."
    guitext "But I was able to see a little bit of red hair, and he wear sunglases."
    guitext "His cloth weren't very special just a plain light brown desert suit like most people have."
    guibutton "Ask more .." "btActionTamara_4"
]

newgui Tamara_8 [
    guitext "I'm not sure, as he went out her drove away with a blue hoverbiker."
    guibutton "Ask more .." "btActionTamara_4"
]

btActionTamara_0 = [
    npc_bye "Tamara" 2
]

btActionTamara_1 = [
    showgui Tamara_2
    tamara_asked = 1
    sleep 120000 [tamara_saw_wierdo = 1] //May altered in future
    sound $speech_tamara_unfortunetly
]

btActionTamara_2 = [
    if (&& (= $tamara_saw_wierdo 1) (< $tamara_things_asked_about_wierdo 4))[
        showgui Tamara_4
        sound $speech_tamara_resently
    ][
        showgui Tamara_3
        sound $speech_tamara_sorry
    ]
]

btActionTamara_3 = [
    showgui Tamara_5
    tamara_things_asked_about_wierdo = (+ $tamara_things_asked_about_wierdo 1)
    tamara_asked_1 = 1
    sound $speech_tamara_Only_to_TC37
]

btActionTamara_4 = [ //Ask more
    showgui Tamara_4
]

btActionTamara_5 = [
    showgui Tamara_6
    tamara_things_asked_about_wierdo = (+ $tamara_things_asked_about_wierdo 1)
    tamara_asked_2 = 1
    sound $speech_tamara_Double_Ice_Goog
]

btActionTamara_6 = [
    showgui Tamara_7
    tamara_things_asked_about_wierdo = (+ $tamara_things_asked_about_wierdo 1)
    tamara_asked_3 = 1
    sound $speech_tamara_Wierdo_descr
]

btActionTamara_7 = [
    showgui Tamara_8
    tamara_things_asked_about_wierdo = (+ $tamara_things_asked_about_wierdo 1)
    tamara_asked_4 = 1
    sound $speech_tamara_Blue_Hoverbike
]
